module DiversiaScript

imports
  libstratego-lib
  libstratego-gpp
  libstratego-aterm
  include/DiversiaScript
  lib/editor-common.generated
  complete
  rename
  store
  desugar
  decorate
  codegen/lua/luagen
  
  lib/namespaces
  namespace
  type-project

  note-check
  error-check
  warning-check
  
strategies
	
	analyse-all = desugar-all; decorate-all; rename-all; store-all

rules // Main editor interface (defined by editor/DiversiaScript-Builders and -References.esv)
  
	// Analyzes the current program, returning a tuple with errors, warnings, and notes;
	// each a list of (term, message) tuples or simply (message) terms.
	editor-analyze: (ast, path, project-path) -> (ast', errors, warnings, notes)
		where
	    	editor-init;
	    	<analyse-all> ast => ast';
			<collect-all(editor-error, conc)> ast' => errors ;
			<collect-all(editor-warning, conc)> ast' => warnings ;
			<collect-all(editor-note, conc)> ast' => notes
			
	// Runs the DefaultClient with lua scripts generated from current AST.
	run: (selected, position, ast, path, project-path) -> (ast)
		where dir := <abspath> <dirname> path
		where clientDir := "F:/MultiverseVS/Git/DefaultClient/bin/ReleaseWin32/"
		where
			editor-init;
	    	(<lua-tofiles(|path)> <to-lua> <analyse-all> ast <+ fatal-err(|$[Lua code generation failed.]));
	    	(<chdir> clientDir <+ fatal-err(|$[Could not cd into [clientDir].]));
	    	(<call> ($[[clientDir]Diversia-DefaultClient-DbgInfo.exe], ["-o", "-m", $["[dir]"], "-s", "gen/GameMode.lua"]) <+ 
	    		fatal-err(|$[Could not start the client or the cliented terminated abnormally.]))
	    		  
	// Transforms analysed AST to Lua.
	generate-lua-files: (selected, position, ast, path, project-path) -> (ast)
		where
			editor-init;
	    	<lua-tofiles(|path)> <to-lua> <analyse-all> ast
	generate-lua-strings: (selected, position, ast, path, project-path) -> (filename, result)
		where
			editor-init;
	    	filename := <guarantee-extension(|"lua")> path;
	    	result   := <lua-tostrings> <to-lua> <analyse-all> ast
	generate-lua-aterm: (selected, position, ast, path, project-path) -> (filename, result)
		where 
	    	editor-init;
	    	filename := <guarantee-extension(|"aterm")> path;
	      	result   := <to-lua> <analyse-all> ast
		
	// Transforms analysed AST to C++.
	generate-cpp: (selected, position, ast, path, project-path) -> (filename, result)
		where
	    	editor-init;
	    	filename := <guarantee-extension(|"cpp")> path;
	    	result   := "NYI"

	// Prints the analysed AST.
	generate-analysed-aterm: (selected, position, ast, path, project-path) -> (filename, result)
		where
			editor-init;
			filename := <guarantee-extension(|"aterm")> path;
			result   := <desugar-all; decorate-all; rename-all> ast
	  	
	// Prints the AST of a selection.
	generate-aterm: (selected, position, ast, path, project-path) -> (filename, result)
		where
			editor-init;
			filename := <guarantee-extension(|"aterm")> path;
			result   := selected
	
	// Hover tooltip
	editor-hover: (node, position, ast, path, project-path) ->  $[Type: [<printable> type]]
		where 
			<desugar-all> ast => ast';
			<desugar-position-innermost(desugar|ast)> position => position';
			<term-at-position(|position')> ast' => node';
			<debug> node';
			<type-of> node' => type
	desugar-position-innermost(desugar|ast): position -> position'
	    where
	      ast'  := <at-position(!<id>{MARKER()}|position)> ast;
	      ast'' := <innermost(preserve-annos({?x; desugar; not(?x)}))> ast';
	      position' := <position-of-term({?_{a*}; <one(?MARKER())> a*})> ast''
