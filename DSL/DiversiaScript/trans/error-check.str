module error-check

imports 
	include/DiversiaScript
	signatures
	type-project
	name-project
	data/builtin-types

rules  
	
	editor-error: e@VarDef(name, type, expr)-> (e, $[Expected [<printable>type] but found [<printable>type2].])
		where 
			<type-of> expr => type2;
			not(<match-type>(type,type2))
	
	editor-error: e@Assign(exp1, exp2) -> (e, $[Expected [<printable>type1] but found [<printable>type2].]) 
		where
			<type-of> exp1 => type1;
			<type-of> exp2 => type2;
			not(<match-type>(type1,type2))
			
	editor-error: e@ObjectProp(prop, expr) -> (e, $[Expected [<printable>type1] but found [<printable>type2].])
		where
			<type-of> prop  => type1;
			<type-of> expr => type2;
			not(<match-type>(type1,type2))
	editor-error: e@AOp(_, _, exp) -> (e, $[Expected Int or Real but found [<printable>type2]])
		where 
			<type-of> exp => type2;
			not(<match-type>(<type-of> e,type2))
			
	editor-error: e@AOp(_, exp, _) -> (e, $[Expected Int or Real but found [<printable>type2]])
		where 
			<type-of> exp => type2;
			not(<match-type>(<type-of> e,type2))
	
	editor-error: e@PropRef(name, expr) -> (e, $[Undefined variable or property.])
		where not(<get-type-property>(<type-of>expr, name))
		
	editor-error: e@Access(expr, name) -> (e, $[Undefined property.])
		where not(<get-type-property>(<type-of>expr, name))
	
	editor-error: e@Call(This(object), name, []) -> (e, $[Undefined function.])
		where not(is-builtin-function(|"ClientObject", name)) // TODO added costum functions
		
	editor-error: e@PropDef(object, name, type, exp) -> (e, $[Overwriting built-in properties not allowed.])
		where is-builtin-property(|"ClientObject", name) 

	editor-error: e@PluginDef(name,CustomType(type),props) -> (e, $[Unknown Plugin.])
		where not(is-builtin-plugin(|type))
			
	editor-error: e@ComponentDef(name,CustomType(type),props) -> (e, $[Unknown Component.])
		where not(is-builtin-component(|type))
			
			
			
			
			
			
	
	